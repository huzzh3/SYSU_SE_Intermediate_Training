# Part 1 问题回答

**18327034 胡泽钊 19级软件工程**

## Question1

### 问题

> Does the bug always move to a new location? Explain.

### 回答

* 不会，**bug并不会一直移动到新的位置**，要弄清楚这个问题，得先明白bug的运行逻辑，bug 的运行逻辑如下：
  * 当前进的方向有石头的时候，bug 会顺时针调整自己的方向 45 度，然后再判断是否能够前进。
  * 当前进的方向有敌人的时候，bug 同样也会顺时针调整自己的行动方向，再判断是否能够前进。
  * 当 bug 前进的方向为地图边缘的时候， bug 也会顺时针调整自己的行动方向，再判断是否能够前进。
  * 当 bug 判断前面无障碍物且能够通过的时候，bug 就会往其正对的方向移动一格。
* **也就是说：**
  * 如果 bug 会找到一条闭途径，该闭途径不包含任何障碍物和地图边缘以外的部分，则 bug 会一直再该闭途径上移动，**而不会再移动到新的位置上！**。
  * **否则 bug 只可能被困在了原地，无法移动。此时同样不会移动到新的位置！**
* 我们用具体的程序结果动图来演示上述两种情况：

![bugMoveAround](https://i.loli.net/2021/10/20/teUwHyc4O6fKIWM.gif)


* 可以看到在上述情况中，bug 一直在地图的外面一圈移动，无论我们运行了多长时间，bug 始终只在地图外面一圈移动，而不会再移动到新的区域去。

![bugMoveAround2](https://i.loli.net/2021/10/20/qrWIedtuE3TolJM.gif)

* 在该情况下，bug 四面八方都有石头，bug无法移动，只能在原地打转，此时 bug 也不能移动到新的地方去。

* 具体的代码实现（无源码，只能分析对应的 jar 文件）如下所示：

```java
  // @file: gridworld.jar/info.gridworld.actor/Bug.class
  // @line: 无标示，只需找到 act() 函数，即可找到如下代码
  
  // Method descriptor #23 ()V
  // Stack: 1, Locals: 1
  public void act();
     0  aload_0 [this]
     1  invokevirtual info.gridworld.actor.Bug.canMove() : boolean [4]
     4  ifeq 14
     7  aload_0 [this]
     8  invokevirtual info.gridworld.actor.Bug.move() : void [5]
    11  goto 18
    14  aload_0 [this]
    15  invokevirtual info.gridworld.actor.Bug.turn() : void [6]
    18  return
```

* 从上方代码可以看到我们的分析出的 bug 的运行逻辑是正确的，在代码中我们可以看到：
  * 先用 Bug 类中的 canMove 函数判断 bug 是否能够前进。
    * 如果不能，则转到 pc == 14 所对应的指令，然后调用 trun 函数进行转向，然后再返回结果
    * 如果能，则调用 move 函数，并调到 pc == 18 对应的指令，即返回结果

---

## Question 2

### 问题

> In which direction does the bug move?

### 回答

* **如果 bug 所正对的方向上没有障碍物且不是地图边缘的话，则 bug 会向它所正对的方向移动。**

* 为了验证我们的回答，我们可以研究 gridWorld.jar 文件中指令是否符合我们的回答。
  * 在第一个问题中，我们曾解释了，bug 在运行 act 函数的时候，会先调用 canMove 函数来判断其是否能够移动，如果可以则移动，如果不行则换方向。
  * 而 canMove 函数的指令如下所示：

```java
  // @file: gridworld.jar/info.gridworld.actor/Bug.class
  // @line: 无标示，只需找到 canMove() 函数，即可找到如下代码

  // Method descriptor #45 ()Z
  // Stack: 2, Locals: 5
  public boolean canMove();
     0  aload_0 [this]
     1  invokevirtual info.gridworld.actor.Bug.getGrid() : info.gridworld.grid.Grid [9]
     4  astore_1 [gr]
     5  aload_1 [gr]
     6  ifnonnull 11
     9  iconst_0
    10  ireturn
    11  aload_0 [this]
    12  invokevirtual info.gridworld.actor.Bug.getLocation() : info.gridworld.grid.Location [10]
    15  astore_2 [loc]
    16  aload_2 [loc]
    17  aload_0 [this]
    18  invokevirtual info.gridworld.actor.Bug.getDirection() : int [7]
    21  invokevirtual info.gridworld.grid.Location.getAdjacentLocation(int) : info.gridworld.grid.Location [11]
    24  astore_3 [next]
    25  aload_1 [gr]
    26  aload_3 [next]
    27  invokeinterface info.gridworld.grid.Grid.isValid(info.gridworld.grid.Location) : boolean [12] [nargs: 2]
    32  ifne 37
    35  iconst_0
    36  ireturn
    37  aload_1 [gr]
    38  aload_3 [next]
    39  invokeinterface info.gridworld.grid.Grid.get(info.gridworld.grid.Location) : java.lang.Object [19] [nargs: 2]
    44  checkcast info.gridworld.actor.Actor [20]
    47  astore 4 [neighbor]
    49  aload 4 [neighbor]
    51  ifnull 62
    54  aload 4 [neighbor]
    56  instanceof info.gridworld.actor.Flower [15]
    59  ifeq 66
    62  iconst_1
    63  goto 67
    66  iconst_0
    67  ireturn
```

* 这个代码的具体逻辑如下**（分析过程略长，可以跳过，下面会有简短的总结）**：
  1. 先调用 getGrid 函数，获取网格信息，并将网格信息保存下来，记为 gr
     1. 如果 gr 不为 null，则往下执行（即跳到 pc == 11 的部分）
     2. 如果 gr 为 null ，那么就直接返回 0，即返回 false
  2. 再调用 getLocation 函数，获取 bug 当前所在的位置，并讲位置保存下来，记为 loc
  3. 之后调用 getDirection 函数，**神奇的是，获取的值没有用任何变量保存下来！**
  4. 然后再调用 getAdjacentLocation 函数，获取 bug 临近的方格的位置，并记为 next，很明显这里作者的源代码应该写的是 **getAdjacentLocation(getDirection(...))**  所以在  getDirection 函数中，才没有用任何变量保存函数的返回值。
  5. 之后用 gr 和 next 作为参数，调用 isValid 函数，判断该 next 所指的位置是否能够用于 bug 的下一步移动。
     1. 如果不能的话，则直接返回 0 
     2. 如果能的话，则继续往下执行
  6. 再使用 gr 和 next 作为参数，获取 next 所指位置上的 actor，并记为 neighbor
     1. 如果 neighbor 为 null 的话，那么就创建一个 flower 的实例
     2. 如果 neighbor 不为 null 的话，则继续执行
  7. 返回 1，程序结束 （最后还有一个返回 0，估计是为了增强程序的鲁棒性所设置的，实际上该返回 0 在正常情况下都不会执行到）。

* **总而言之，就是用 getDirection 函数调用 bug 的面对的方向，然后判断其能否移动，如果可以就移动；如果不行，就转向。**
* 用程序运行的具体情况来说明：

![bugMoveOneStep](https://i.loli.net/2021/10/20/GW3AUyeHbn7uvzq.gif)

* 首先程序被初始化，bug 在地图的一侧。

* 然后点击 step 按钮，bug 向其面对的方向移动了一格。

---

## Question 3

### 问题

> What does the bug do if it does not move?

### 回答

* **当 bug 不能够移动的时候（指的是它所面对的方向无法作为它的下一步的移动方向），它就会选择顺时针转 45 度，得到新的移动方向，然后判断新的移动方向是否能让它移动。**

* 为了验证我们的回答，同样我们需要去找到源代码来验证我的想法，在第一问中，我们发现如果 bug 不能移动的话，那么它就会调用 turn 函数，turn 函数的源代码如下：

```java
  // @file: gridworld.jar/info.gridworld.actor/Bug.class
  // @line: 无标示，只需找到 canMove() 函数，即可找到如下代码
  
  // Method descriptor #23 ()V
  // Stack: 3, Locals: 1
  public void turn();
     0  aload_0 [this]
     1  aload_0 [this]
     2  invokevirtual info.gridworld.actor.Bug.getDirection() : int [7]
     5  bipush 45
     7  iadd
     8  invokevirtual info.gridworld.actor.Bug.setDirection(int) : void [8]
    11  return
```

* 可以看到，该代码非常简短，其主要逻辑如下：
  * 首先用 getDirection 函数来获取 bug 现在所面对的方向
  * 然后使用**自增运算**，得到新的方向
  * 最后再用新的方向来设置 bug 的方向即可

* 总而言之，我们的回答是合理的，我们再用动图来演示该过程：

![bugTurn](https://i.loli.net/2021/10/20/RINeQU6t1yZO87w.gif)

---

## Question 4

### 问题

> What does a bug leave behind when it moves?

### 回答

* **每次 bug 向前移动一格之后，它都会在原来的位置上留下一朵花。**
* 其中机制的源码和分析如下所示：

```java
  // @file: gridworld.jar/info.gridworld.actor/Bug.class
  // @line: 无标示，只需找到 move() 函数，即可找到如下代码

  // Method descriptor #23 ()V
  // Stack: 3, Locals: 5
  public void move();
     0  aload_0 [this]
     1  invokevirtual info.gridworld.actor.Bug.getGrid() : info.gridworld.grid.Grid [9]
     4  astore_1 [gr]
     5  aload_1 [gr]
     6  ifnonnull 10
     9  return
    10  aload_0 [this]
    11  invokevirtual info.gridworld.actor.Bug.getLocation() : info.gridworld.grid.Location [10]
    14  astore_2 [loc]
    15  aload_2 [loc]
    16  aload_0 [this]
    17  invokevirtual info.gridworld.actor.Bug.getDirection() : int [7]
    20  invokevirtual info.gridworld.grid.Location.getAdjacentLocation(int) : info.gridworld.grid.Location [11]
    23  astore_3 [next]
    24  aload_1 [gr]
    25  aload_3 [next]
    26  invokeinterface info.gridworld.grid.Grid.isValid(info.gridworld.grid.Location) : boolean [12] [nargs: 2]
    31  ifeq 42
    34  aload_0 [this]
    35  aload_3 [next]
    36  invokevirtual info.gridworld.actor.Bug.moveTo(info.gridworld.grid.Location) : void [13]
    39  goto 46
    42  aload_0 [this]
    43  invokevirtual info.gridworld.actor.Bug.removeSelfFromGrid() : void [14]
    46  new info.gridworld.actor.Flower [15]
    49  dup
    50  aload_0 [this]
    51  invokevirtual info.gridworld.actor.Bug.getColor() : java.awt.Color [16]
    54  invokespecial info.gridworld.actor.Flower(java.awt.Color) [17]
    57  astore 4 [flower]
    59  aload 4 [flower]
    61  aload_1 [gr]
    62  aload_2 [loc]
    63  invokevirtual info.gridworld.actor.Flower.putSelfInGrid(info.gridworld.grid.Grid, info.gridworld.grid.Location) : void [18]
    66  return
```

* 我们可以看到 move 函数的运行过程如下：
  1. 首先，调用 getGrid 函数，将整个地图的网格信息记录下来，并保存在变量 gr 中。
     	1. 如果 gr 为 null，则函数直接返回。
      	2. 如果 gr 不为 null，那么函数就会跳到第 10 条指令的位置，即继续往下执行。
  2. 然后程序调用 getLocation 函数，记录下 bug 的位置，并保存在变量 loc 中。
  3. 之后程序调用 getDirection 函数，记录下 bug 现在所面对的方向。
  4. 之后直接用刚才得到的方向，作为参数，带入 getAdjacentLocation 函数，直接得到 bug 所应该移动到的下一个位置，并将该位置记录在变量 next 中。
  5. 然后将 gr 和 next 作为参数，调用 isValid 函数，判断该位置是否合法。
     1. 如果不合法，则跳到第 42 条语句，在第 42 条语句后面，会调用 removeSelfFromGrid 函数，将 bug 直接从 地图上移除。**（也就是说，如果你的 bug 前面是不可移动的目标，你仍然对它单击，让它执行 move 函数的话，它就会直接从地图移除，具体情况可以参考下面的动图）**
     2. 如果合法，那么就继续往下执行。
  6. 若此时合法，之后调用 moveTo 函数，将 bug 移动到下一个位置，然后跳转到 46 条语句 （也就是跳过了 removeSelfFromGrid 函数，防止在合法的情况下移除 bug）
  7. 之后第 46 条语句之后会创建一个 Flower 的实例。
  8. 再然后，会调用 getColor 函数，获取 bug 的颜色，然后将该颜色，赋给 Flower 实例，使其颜色和 bug 的颜色相同。
  9. 之后将创造的 Flower 实例用 flower 变量保存下来，再用 putSelfInGrid 函数，将花朵放在地图上，至此程序结束。
* 接下来，我们可以再次使用第二问的动图，来演示每次 bug 移动，都会在身后留下一个 flower：

![bugMoveOneStep](https://i.loli.net/2021/10/20/GW3AUyeHbn7uvzq.gif)

* 之后我们再来演示，如果 bug 前面不是有效位置，我们坚持让 bug 往下移动，会发生什么：

![bugRemove](https://i.loli.net/2021/10/20/v2iR4bw1qGsPrTI.gif)

* 可以看到，如果让 bug 强制移动，它就会将自己从地图上移除。

---

## Question 5

### 问题

> What happens when the bug is at an edge of the grid? (Consider whether  the bug is facing the edge as well as whether the bug is facing some  other direction when answering this question.)

### 回答

* 先说结论：
  * 如果 bug 正对着地图边缘的话，则它至少会转两次方向。
  * 如果 bug 正对方向与地图边缘成 45 度角的话，则它至少会转一次方向。
  * 如果 bug 正对方向与地图边缘成 90 度角的话，则它可能会一次方向都不用旋转。
* 注意到我这里使用的是**“不确定描述”**，我说的是“至少”和“可能”，具体的原因稍后会解释。
* 那么造成这个结果的原因也很简单，结合一下我们前面所分析的源代码：
  * 每次 bug 旋转方向的时候，它只会顺时针旋转 45 度角。
  * 这也就导致了，如果它正对的是地图边缘，那么它旋转一次后，它的前进方向和地图边缘呈 45 度角，仍然不能移动，故它还要再旋转一次，才可能得到可以移动的位置。
  * 同样道理，如果它是45度角对着地图边缘的话，那么它一定要旋转一次，才可能得到可以移动的位置。
  * 那么如果它与地图呈90度角的话，那么它可能可以直接移动，而不需要旋转。
* 上述三种情况如动图所示：
* 当 bug 正对着地图边缘的时候：

![bugAndEdge](https://i.loli.net/2021/10/20/duFESlo2w1bhjKB.gif)

* 当 bug 与地图边缘呈 45 度角的时候：

![bugAndEdge2](https://i.loli.net/2021/10/20/IpaJL2URqON18Hl.gif)

* 当 bug 和地图边缘呈 90 度角的时候：

![bugAndEdge3](https://i.loli.net/2021/10/20/Y6125Cek4sG9VaL.gif)

* 那么上面三张动图，很好的说明了我们的结论是正确的。

* 接下来还要说明，为什么我们上面说的是“至少”和“可能”，理由如下

![bugAndEdge4](/home/arnohu/Pictures/SeTraining/bugAndEdge4.gif)

* 在上述情况中，bug 转向了 3 次 才找到了可以移动的下一个位置，而不只是 2 次，所以上述结论中，我们用的是“可能”和“至少”。

---

## Question 6

### 问题

> What happens when a bug has a rock in the location immediately in front of it?

### 回答

* 如果 bug 前面立刻出现了一个石头，它也同样会至少转向一次，去寻找下一个可以移动的位置，而不会有什么其他的特殊的运动逻辑。
* 从前面我们的分析，不难得出原因：bug 的运行逻辑是一步步进行的，当它前面立刻出现一块石头，它还是会先判断前面的位置是否适合移动，很明显它会判断出不合适，然后它就会转向，然后再去判断下一个位置是否适合去移动。

---

## Question 7

### 问题

> Does a flower move?

### 回答

* flower 不能移动，为了确认 flower 真的不可以移动，我们可以去 jar 文件中找到对应的源码，如下所示：

```java
  // gridworld.jar/info.gridworld.actor/Flower.class
  // @line: 无标示，只需找到 act() 函数，即可找到如下代码

  // Method descriptor #23 ()V
  // Stack: 6, Locals: 5
  public void act();
     0  aload_0 [this]
     1  invokevirtual info.gridworld.actor.Flower.getColor() : java.awt.Color [4]
     4  astore_1 [c]
     5  aload_1 [c]
     6  invokevirtual java.awt.Color.getRed() : int [5]
     9  i2d
    10  ldc2_w <Double 0.95> [6]
    13  dmul
    14  d2i
    15  istore_2 [red]
    16  aload_1 [c]
    17  invokevirtual java.awt.Color.getGreen() : int [8]
    20  i2d
    21  ldc2_w <Double 0.95> [6]
    24  dmul
    25  d2i
    26  istore_3 [green]
    27  aload_1 [c]
    28  invokevirtual java.awt.Color.getBlue() : int [9]
    31  i2d
    32  ldc2_w <Double 0.95> [6]
    35  dmul
    36  d2i
    37  istore 4 [blue]
    39  aload_0 [this]
    40  new java.awt.Color [10]
    43  dup
    44  iload_2 [red]
    45  iload_3 [green]
    46  iload 4 [blue]
    48  invokespecial java.awt.Color(int, int, int) [11]
    51  invokevirtual info.gridworld.actor.Flower.setColor(java.awt.Color) : void [3]
    54  return
```

* 可以看到 flower 的 act 函数的运行逻辑如下：
  * 首先用 getColor 函数，获取到 flower 的颜色，并将颜色记录在变量 c 中
  * 然后用 c 作为参数，调用 getRed 函数，获取到该颜色的红色分量，并将红色分量乘上 0.95
    * 同样道理用 getGreen 和 getBlue 函数，获取到该颜色的绿色分量和蓝色分量，同样将蓝色分量和绿色分类乘上 0.95
  * 最后混合三种颜色，得到新的颜色，用 setColor 函数，将新的颜色设置给 flower，函数结束

* **也就是说：flower 的运行逻辑不包含移动功能，故 flower 不能移动**

---

## Question 8

### 问题

> What behavior does a flower have?

### 回答

* 从上一个问题中，我们分析得出，flower 的 act 函数中的运行逻辑是：
  * 获取到花朵当前颜色的RGB分量
  * 将各个分量乘上 0.95，得到一个新的颜色
  * 将新的颜色设置给 flower

---

## Question 9

### 问题

> Does a rock move or have any other behavior?

### 回答

* **rock 不能移动，且它没有别的行为功能**，理由如下：

```java
  // gridworld.jar/info.gridworld.actor/Rock.class
  // @line: 无标示，只需找到 act() 函数，即可找到如下代码
  
  // Method descriptor #10 ()V
  // Stack: 0, Locals: 1
  public void act();
    0  return
```

* 可以看到 rock 的 act 函数里面，只有一个 return 的指令，而没有其他的功能

---

## Question 10

### 问题

> Can more than one actor (bug, flower, rock) be in the same location in the grid at the same time?

### 回答

* **程序不允许多个目标处于同时处于同一个位置。**

* 理由如下：
  * rock 和 flower 不能移动，故它们不可能主动与已有的 actor 存在同一个位置
  * 而 bug 能够移动，故它可能与已有的 actor 存在同一位置：
    * 当 bug 遇到障碍物（石头或者地图边缘）的时候，它会选择转向，避免自己撞上障碍物
    * 当 bug 遇到 flower 的时候，就会直接覆盖 flower，即移除下一个位置的 flower ，并在下一个位置加上 bug 实例
  * 故 bug 也不会与别的实例处于同一位置

* 我们还可以从源码来分析，为什么不可能多个目标存在同一个位置：

```java
  // gridworld.jar/info.gridworld.actor/ActorWorld
  // @line: 无标示，只需找到 putSelfInGrid() 函数，即可找到如下代码

  public void putSelfInGrid(info.gridworld.grid.Grid gr, info.gridworld.grid.Location loc);
     0  aload_0 [this]
     1  getfield info.gridworld.actor.Actor.grid : info.gridworld.grid.Grid [5]
     4  ifnull 17
     7  new java.lang.IllegalStateException [7]
    10  dup
    11  ldc <String "This actor is already contained in a grid."> [8]
    13  invokespecial java.lang.IllegalStateException(java.lang.String) [9]
    16  athrow
    17  aload_1 [gr]
    18  aload_2 [loc]
    19  invokeinterface info.gridworld.grid.Grid.get(info.gridworld.grid.Location) : java.lang.Object [10] [nargs: 2]
    24  checkcast info.gridworld.actor.Actor [11]
    27  astore_3 [actor]
    28  aload_3 [actor]
    29  ifnull 36
    32  aload_3 [actor]
    33  invokevirtual info.gridworld.actor.Actor.removeSelfFromGrid() : void [12]
    36  aload_1 [gr]
    37  aload_2 [loc]
    38  aload_0 [this]
    39  invokeinterface info.gridworld.grid.Grid.put(info.gridworld.grid.Location, java.lang.Object) : java.lang.Object [13] [nargs: 3]
    44  pop
    45  aload_0 [this]
    46  aload_1 [gr]
    47  putfield info.gridworld.actor.Actor.grid : info.gridworld.grid.Grid [5]
    50  aload_0 [this]
    51  aload_2 [loc]
    52  putfield info.gridworld.actor.Actor.location : info.gridworld.grid.Location [6]
    55  return
```

* 当我们选择在网格的某个位置上放置某个物体的时候，就会调用上述函数。
* 在上述函数中，我们可以看到，它会调用 get 函数，获取当前位置中的实例
* 然后将该实例与 actor 进行对比，检查该实例是否为 actor
  * 如果是 actor ，那么它就会将该 actor 移除
  * 否则就继续执行
* 然后将要防止的 actor 放到对应的网格中，并记录相关信息在网格中。

* **也就是说，如果想要在同一个位置放置多个 actor，新的actor会先将旧的actor删去之后，才会放置新的actor。**
* 故不可能在同一时间的同一位置存在多个 actor
